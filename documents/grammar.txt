stmt_list : (stmt)*

stmt :  {FUNC} FUNC ID LPAREN formal_args? RPAREN stmt
     |  {BOOL_TYPE,INT_TYPE,FLOAT_TYPE,FRACT_TYPE,STRING_TYPE} data_type ID ({ASSIGN} ASSIGN exp)?
     |  {ID} ID id_suffix
     |  {OUT} OUT exp
     |  {RETURN} RETURN exp
     |  {WHILE} WHILE exp stmt
     |  {IF} IF exp stmt (ELSE stmt)?
     |  {FOR} FOR exp COLON exp COLON exp stmt
     |  {DO} DO stmt_list END

data_type :  {BOOL_TYPE} BOOL_TYPE
          |  {INT_TYPE} INT_TYPE
          |  {FLOAT_TYPE} FLOAT_TYPE
          |  {FRACT_TYPE} FRACT_TYPE
          |  {STRING_TYPE} STRING_TYPE

id_suffix :  {LPAREN} LPAREN actual_args? RPAREN
          |  {ASSIGN} ASSIGN exp

exp :  {BOOL,INT,FLOAT,FRACT,STRING,ID,LPAREN,MINUS,NOT,IN,LBRACKET} exp_low
exp_low :  {BOOL,INT,FLOAT,FRACT,STRING,ID,LPAREN,MINUS,NOT,IN,LBRACKET} exp_med ((EQ|LE) exp_med)*
exp_med :  {BOOL,INT,FLOAT,FRACT,STRING,ID,LPAREN,MINUS,NOT,IN,LBRACKET} exp_high ((PLUS|MINUS) exp_high)*
exp_high :  {BOOL,INT,FLOAT,FRACT,STRING,ID,LPAREN,MINUS,NOT,IN,LBRACKET} primary ((MUL|DIV) primary)*

primary :  {BOOL} BOOL
        |  {INT} INT
        |  {FLOAT} FLOAT
        |  {FRACT} FRACT
        |  {STRING} STRING
        |  {ID} ID (LPAREN actual_args RPAREN)?
        |  {LPAREN} LPAREN exp RPAREN
        |  {MINUS} MINUS primary
        |  {NOT} NOT primary
        |  {IN} IN STRING?
        |  {LBRACKET} LBRACKET (exp (COMMA exp)*)? RBRACKET

formal_args :  {BOOL_TYPE,INT_TYPE,FLOAT_TYPE,FRACT_TYPE,STRING_TYPE} data_type ID (COMMA data_type ID)*
actual_args :  {BOOL,INT,FLOAT,FRACT,STRING,ID,LPAREN,MINUS,NOT,IN,LBRACKET} exp (COMMA exp)*

ID :  <any valid variable name>
BOOL :  True | False
INT :  <any valid int number>
FLOAT :  <any valid floating point number>
FRACT :  <any valid fractional number>
STRING :  <any valid quoted str constant>
